" Author: Theerawat Kiatdarakun
" TODO combine mapping sections
source ~/.vimrc.const

" test: https://stackoverflow.com/questions/37644682/why-is-vim-so-slow
" set timeoutlen=1000
" set ttimeoutlen=0

let template_dir='~/.vim/template/'
" TODO use vim tag sections here also check mapping first perhaps can remove leader
" ========================================
" Set
" ========================================
let @/ = "" " clear last search pattern
set autoindent " auto indent next line
set backspace=indent,eol,start " make backspace to work
set completeopt-=preview " disable scratch preview
set cursorline " highlight current line
" directories
set backupdir=~/.vim/backup//
set directory=~/.vim/swap//
set undodir=~/.vim/undo//

set expandtab " auto-indent with spaces instead of tabs
set foldmethod=indent
set incsearch " highlight while still typing search
set list " enable listchars
set listchars=eol:$,tab:\|-,trail:_
set nofixendofline
set nofoldenable " disable auto-folding at start
set nohls
set number " show line number
set shellcmdflag=-ic
" netrw
let g:netrw_browse_split = 3 " open file in new tab
let g:netrw_bufsettings = 'noma nomod nu nobl nowrap ro nornu' " enable line number
let g:netrw_liststyle = 3 " tree listing style"

set pumheight=10 " Pmenu max height
exe 'set shiftwidth='.space_per_tab
exe 'set tabstop='.space_per_tab
" syntax highlighting
syntax reset
syntax on
" ========================================
" automatic command
" ========================================
" markdown - remove highlight on error
autocmd BufNewFile,BufRead,BufEnter *.md :syn match markdownError "\w\@<=\w\@="
" tab - move new tab to the last position
autocmd BufNew * execute ":tabm"
" ========================================
" mapping automatically
" ========================================
" {([: generate closing braces automatically
for val in ['{}','()','[]']
	exe 'inoremap '.val[0].' '.val[0].val[1].'<Esc>i'
	exe 'inoremap '.val[0].'<CR> '.val[0].'<CR>'.val[1].'<Esc>ko'
endfor
" placeholder
inoreabbrev lorem Lorem ipsum dolor sit amet, consectetur adipiscing elit. In dapibus convallis massa, nec gravida diam pellentesque at. Aliquam mollis tempor mi sed venenatis. Maecenas neque massa, pulvinar vitae lacus et, convallis interdum ligula. Suspendisse rhoncus a arcu quis volutpat. Donec ac risus eros. Pellentesque convallis lectus eu sodales ornare. Quisque egestas ex non purus porta porttitor. Ut blandit feugiat iaculis. Nulla mollis venenatis pulvinar. Nullam pulvinar efficitur aliquet. Donec in nibh eleifend, finibus dui nec, faucibus dui.
" help - start in a new tab
cnoremap help tab help
" shebang
inoreabbrev shebang #!/usr/bin/env
" ========================================
" mapping manually with <leader>
" ========================================
" 0-8: go to tab, 9: go to last tab
for val in range(0,8)
	exe 'nnoremap <leader>'.val.' '.val.'gt<cr>'
endfor
nnoremap <leader>9 :tabl<cr>
" {(["'*<`: enclose curent word with its pair
" TODO custom pair
for val in ['{}','()','[]','""',"''",'**','<>','``']
	exe 'nnoremap <leader>'.val[0].' ciw'.val[0].val[1].'<esc>Pl'
	exe 'vnoremap <leader>'.val[0].' mq:s/\%V.*\%V./'.val[0].'&'.val[1].'/<cr>`qf'.val[1].':nohl<cr>'
	if val[0] !=# val[1]
		exe 'nnoremap <leader>'.val[1].' ciw'.val[0].val[1].'<esc>Pl'
		exe 'vnoremap <leader>'.val[1].' mq:s/\%V.*\%V./'.val[0].'&'.val[1].'/<cr>`qf'.val[1].':nohl<cr>'
	endif
endfor
" ** enclose word
nnoremap <leader>** ciw****<esc>hPl
vnoremap <leader>** mq:s/\%V.*\%V./**&**/<cr>`qf'**':nohl<cr>
" /: search current word
nnoremap <leader>/ viw"qy/<c-r>q<cr>N
vnoremap <leader>/ y/<c-r>"<cr>
" space: surround a word with spaces
nnoremap <leader><space> mqbi<space><esc>ea<space><esc>`ql
" c: colorscheme
nnoremap <leader>c0 :colorscheme default<cr>
nnoremap <leader>c1 :colorscheme custom-dark<cr>
nnoremap <leader>c2 :colorscheme custom-light<cr>
" c: comment
for i in range(0,5)
	exe ':autocmd BufNewFile,BufRead '.my_commentgroup[i]['ext'].' nnoremap <buffer> <leader>c '.my_commentgroup[i]['comment-line']
	exe ':autocmd BufNewFile,BufRead '.my_commentgroup[i]['ext'].' vnoremap <buffer> <leader>c '.my_commentgroup[i]['comment-visual']
endfor
" c: comment - manual
nnoremap <leader>cc ^i/*<esc>A*/<esc>
nnoremap <leader>c/ ^i//<esc>A<esc>
nnoremap <leader>c{ ^i{/*<esc>A*/}<esc>
" c: copy - current file
  " nnoremap <leader>cf :!cat % \| xclip -selection clipboard<cr><cr>
" d: definition highlight group
map <leader>def :echo "hi<" . synIDattr(synID(line("."),col("."),1),"name") . '> trans<'
			\ . synIDattr(synID(line("."),col("."),0),"name") . "> lo<"
			\ . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name") . ">"<CR>
" d: delete - braces pair
nnoremap <leader>d %x``x
" d: delete - first letter
nnoremap <leader>d^ mqI<del><esc>`qh
" d: delete - last character in line
nnoremap <leader>da mq$x<esc>`q
" d: delete - trailing character(s)
for char in [';','$']
	exe 'nnoremap <leader>d'.char.' mq:%s/'.char.'$//g<cr>`q'
endfor
nnoremap <leader>d<Space> mq:%s/\s\+$//<cr>`q
" e: escape
nnoremap <leader>e :call EscapeHTML()<cr>"
" f: file - print current
nnoremap <leader>f :execute "normal! a" . expand('%:t:r')<CR>
" h: highlight - toggle
nnoremap <expr> <leader>hl (&hls && v:hlsearch ? ':nohls' : ':set hls')."\n"
" i: insert - a character at the end of the line
for char in [',',';']
	exe 'nnoremap <leader>i'.char.' mqA'.char.'<esc>`q'
endfor
" o: open file
nnoremap <leader>obc :vsplit ~/.bashrc<cr>
nnoremap <leader>obs :vsplit ~/.bashrc.shared<cr>
nnoremap <leader>ot :tabnew ~/.vim/template/<cr>
nnoremap <leader>ovc :vsplit ~/.vimrc<cr>
nnoremap <leader>ovs :vsplit ~/.vimrc.shared<cr>
nnoremap <leader>ozc :vsplit ~/.zshrc<cr>
nnoremap <leader>ozs :vsplit ~/.zshrc.shared<cr>
nnoremap <leader>oze :vsplit ~/.zshenv<cr>
nnoremap <leader>ozp :vsplit ~/.zprofile<cr>
" oq: open file in qutebrowser
nnoremap <leader>oq :!qutebrowser %<cr>
" p: paste from clipboard to end of file
nnoremap <leader>pc :w<cr>:!echo $(xclip -o selection clipboard) >> %<cr>L<cr>
" TODO vnoreamp '<,'>s/\%Vfoo\%V/bar/g
" r: replace - 2 tabs with 1 tab
vnoremap <leader><tab><tab> :s/\%V\t\t/\t/g<cr>:nohl<cr>
" r: replace - all words in file
nnoremap <leader>R viw"qy:%s/<C-r>q/<C-r>q/g<left><left>
vnoremap <leader>R y:%s/<c-r>"/<c-r>"/g<left><left>
" r: replace - all words in line
nnoremap <leader>r viw"qy:s/<C-r>q/<C-r>q/g<left><left>
vnoremap <leader>r y:s/<c-r>"/<c-r>"/g<left><left>
" r: replace - hyphen and underscore with space
nnoremap <leader>r<space> mq:s/[-_]\+/ /g<cr>`q
" r: replace - capitalize first letter of each word
nnoremap <leader>rU mq:s/\<./\u&/g<cr>`q
" r: replace - spaces at front with tab(s)
exe 'nnoremap <leader>r<tab> mq:%s/\(^ *\)\@<= \{'.space_per_tab.'\}/<tab>/g<cr>`q'
" m: move - tab
for val in range(0,9)
	exe 'nnoremap <leader>m'.val.' :tabm'.val.'<CR>'
endfor
" q: quit
nnoremap <leader>q :q<cr>
" s: save file
nnoremap <leader>s :update<cr>
vnoremap <leader>s <c-c>:update<cr>
" s: set - colorcolumn
function! ColHighlight()
  let x = 0
  function! Bar() closure
    let x = 1 - x
    if x !=# 0
      exe ':set colorcolumn='.col('.')
    else
      :set colorcolumn=
    endif
    return x
  endfunction
  return funcref('Bar')
endfunction
let ColHighlightInstance = ColHighlight()
nnoremap <leader>sc :call ColHighlightInstance()<cr>
nnoremap <leader>scc :set colorcolumn=
" s: source file
nnoremap <leader>sv :source $MYVIMRC<cr>
" t: tab new
nnoremap <leader>t :tabedit<cr>
" t: template
exe 'nnoremap <leader>tc <ESC>:r '.template_dir.'c.template<CR>kdd'
exe 'nnoremap <leader>ta <ESC>:r '.template_dir.'asciidoctor.template<CR>kdd'
exe 'nnoremap <leader>tgm <ESC>:r '.template_dir.'main.go.template<CR>kddjjjjjjA'
exe 'nnoremap <leader>tgt <ESC>:r '.template_dir.'main_test.go.template<CR>kddjjjjjjeea'
exe 'nnoremap <leader>th <ESC>:r '.template_dir.'html5.template<CR>kdd/body<CR>:nohl<CR>o'
exe 'nnoremap <leader>tv <ESC>:r '.template_dir.'vue.template<CR>kdd'
" u: uncomment
for i in range(0,len(g:my_commentgroup)-1)
	exe ':autocmd BufNewFile,BufRead '.g:my_commentgroup[i]['ext'].' nnoremap <buffer> <leader>u '.g:my_commentgroup[i]['uncomment-line']
	exe ':autocmd BufNewFile,BufRead '.g:my_commentgroup[i]['ext'].' vnoremap <buffer> <leader>u '.g:my_commentgroup[i]['uncomment-visual']
endfor
" u: comment - manual
nnoremap <leader>uc ^xx<esc>$xx<esc>
nnoremap <leader>u/ ^xx<esc>$<esc>
nnoremap <leader>u{ ^xxx<esc>$xxx<esc>
" y: yank: whole html tag
nnoremap <leader>y< vf>f>y
nnoremap <leader>y> vf>f>y
" ========================================
"                              Scope: File
" ========================================
" asciidoctor autoconvert after save, note! must open file directly like vim <filename>, not vim .
let s:asciidoctor_stylesdir='/home/'.$USER.'/Git/assets/stylesheets/asciidoctor-custom/'
function AsciiDocToHTML()
	execute '!asciidoctor -a stylesdir='.s:asciidoctor_stylesdir.' '.expand('%:p')
	execute '!html-minifier --collapse-whitespace --remove-comments --remove-optional-tags --remove-redundant-attributes --remove-script-type-attributes --remove-tag-whitespace --use-short-doctype --minify-css true --minify-js true -o '.expand('%:r').'.html '.expand('%:r').'.html'
	execute '!echo "'.strftime("%Y-%m-%d %X \(GMT %Z\)").'" > '.expand('%:p:h').'/modified'
	execute '![ \! -f created ] && echo "'.strftime("%Y-%m-%d %X \(GMT %Z\)").'" > '.expand('%:p:h').'/created'
endfunction
autocmd BufWritePost article.adoc :call AsciiDocToHTML()
" ========================================
"               Scope: Configs and Plugins
" ========================================
" plugin: NERDTree =======================
filetype plugin indent on
let NERDTreeShowHidden=1
let NERDTreeShowLineNumbers=1
autocmd StdinReadPre * let s:std_in=1
autocmd VimEnter * if argc() == 0 && !exists("s:std_in") | NERDTree | endif
" ----------------------------------------
" plugin: vim-go =========================
nnoremap <leader>gi :GoImport <C-R><C-W><cr>
" ----------------------------------------
" plugin: vim-terraform ==================
let g:terraform_fmt_on_save=1
" ----------------------------------------
